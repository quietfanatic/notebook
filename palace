#!/usr/bin/perl
package palace;
use v5.18;
use warnings;
use bytes;  # Assume everything is UTF-8
use JSON::MaybeXS ':all';
use Time::HiRes;

sub null () { undef }
sub true () { JSON->true }
sub false () { JSON->false }

sub croak { require Carp; goto &Carp::croak; }
sub clone { require Clone; goto &Clone::clone; }

 # Overridden for testing
our $datadir = 'data';
our $test_time;


##### Misc utilities
sub arity_miss {
    my ($got, $wanted) = @_;
    my (undef, undef, undef, $name) = caller 1;
    return $got > $wanted
        ? "Too many parameters passed to $name ($got > $wanted)"
        : "Too few parameters passed to $name ($got < $wanted)";
}

sub slurp {
    my ($fn) = @_;
    @_ == 1 or croak arity_miss 0+@_, 1;
    local $/;
    open my $F, '<', $fn or die "Failed to open $fn for reading: $!\n";
    my $s = <$F> // die "Failed to read from $fn: $!\n";
    close $F or die "Failed to close $fn: $!\n";
    return $s;
}

sub splat {
    my ($fn, $str) = @_;
    @_ == 2 or croak arity_miss 0+@_, 1;
    open my $F, '>', $fn or die "Failed to open $fn for writing: $!\n";
    print $F $str or die "Failed to write to $fn: $!\n";
    close $F or die "Failed to close $fn: $!\n";
}

sub mkdirs {
    for (@_) {
        next if -d $_;
        mkdir $_ or die "Failed to mkdir $_: $!\n";
        chmod 0770, $_ or die "Failed to chmod $_: $!\n";
    }
}

sub now { defined $test_time ? $test_time++ : time }
sub now_hires { defined $test_time ? ($test_time++, 123456) : Time::HiRes::gettimeofday() }
sub iso_datetime {
    return sprintf '%04d-%02d-%02dT%02d:%02d:%02dZ',
        $_[5]+1900, $_[4]+01, $_[3], $_[2], $_[1], $_[0];
}
sub file_datetime {
    return sprintf '%04d-%02d-%02d_%02d-%02d-%02d',
        $_[5]+1900, $_[4]+01, $_[3], $_[2], $_[1], $_[0];
}
sub new_event_id {
    my ($s, $us) = now_hires;
    return sprintf '%s_%06d', file_datetime(gmtime($s)), $us;
}


##### Validation

sub schema_char {
    my ($schema, $char) = @_;
    return ref $schema eq 'HASH' ? exists $schema->{$char}
         : ref $schema eq 'ARRAY' ? @$schema >= 2 && index($schema->[1], $char) != -1
         : index($schema, $char) != -1;
}

 # Returns list of strings.
sub criticize {
    my ($obj, $schema, $path) = @_;
    @_ < 2 and croak arity_miss 0+@_, 2;
    @_ < 3 and $path = 'TOP' if @_ < 3;
    @_ > 3 and croak arity_miss 0+@_, 3;
    if (!defined $schema) {
        return "Schema itself is broken at $path";
    }
    elsif (!defined $obj) {
        schema_char($schema, '?') or return "Value at $path should not be null";
        return ();
    }
    elsif (ref $schema eq 'HASH') {
        ref $obj eq 'HASH' or return "Value at $path is not an object";
        my @errs;
        for (grep schema_char($schema->{$_}, '!'), keys %$schema) {
            if (!exists $obj->{$_}) {
                push @errs, "Missing required property $_ at $path";
            }
        }
        for (keys %$obj) {
            if (exists $schema->{$_}) {
                push @errs, criticize($obj->{$_}, $schema->{$_}, "$path.$_");
            }
            elsif (exists $schema->{'*'}) {
                push @errs, criticize($obj->{$_}, $schema->{'*'}, "$path.$_");
            }
            else {
                push @errs, "Unallowed property $_ at $path";
            }
        }
        return @errs;
    }
    elsif (ref $schema eq 'ARRAY') {
        ref $obj eq 'ARRAY' or return "Value at $path is not an array";
        my @errs;
        for (0..$#$obj) {
            push @errs, criticize($obj->[$_], $schema->[0], "$path\[$_]");
        }
        return @errs;
    }
    elsif ($schema =~ /name/) {
        ref $obj eq '' or return "Value at $path is not a string, let alone a name";
        my @errs;
        if ($obj eq '') {
            push @errs, "Value at $path is too empty to be a name";
        }
        if (length $obj > 200) {
            push @errs, "Value at $path is too long in bytes to be a name (".length($obj)." > 200)";
        }
        if ($obj =~ /^\s/) {
            push @errs, "Value at $path has leading whitespace, and so cannot be a name";
        }
        if ($obj =~ /\s$/) {
            push @errs, "Value at $path has trailing whitespace, and so cannot be a name";
        }
        if ($obj =~ /[\x00-\x1f\x7f]/) {
            push @errs, "Value at $path contains unprintable characters not allowed in a name";
        }
        return @errs;
    }
    elsif ($schema =~ /datetime/) {
        ref $obj eq '' or return "Value at $path is not a string, let alone a date";
         # Only taking UTC dates
        $obj =~ /^\d\d\d\d-\d\d-\d\dT\d\d:\d\d:\d\dZ$/
            or return "Value at $path does not look like a date";
        return ();
    }
    elsif ($schema =~ /uri/) {
        ref $obj eq '' or return "Value at $path is not a string, let along a uri";
        $obj =~ /^[a-z]+:\/\// or return "Value at $path does not look like a uri";
        return ();
    }
    elsif ($schema =~ /string/) {
        ref $obj eq '' or return "Value at $path is not a string";
         # Not caring about numbers
        return ();
    }
    elsif ($schema =~ /integer/) {
        ref $obj eq '' or return "Value at $path is not a scalar, let alone an integer";
        is_bool($obj) and return "Value at $path is a boolean, not an integer";
        $obj =~ /^\d+$/ or return "Value at $path is not an integer";
        return ();
    }
    elsif ($schema =~ /\*/) {
        return ();  # Anything goes
    }
    else {
        return "Schema itself is broken at $path";
    }
}
sub criticize_die {
    my ($obj, $schema, $mess) = @_;
    @_ == 3 or croak arity_miss 0+@_, 3;
    my @errs = criticize $obj, $schema;
    if (@errs) {
        die join("\n    ", $mess, @errs) . "\n";
    }
}


##### Items

our $item_schema = {
    '?' => true,  # Deleted if null
    name => 'name!',
     # Mandatory
    tags => ['name', '!'],
    contents => ['string', '!'],
     # Optional
    title => 'string',
    origin => {
        uri => 'uri!',
        originated_at => 'datetime',
    },
    misc => { '*' => '*?' },
     # Things not manually writable (changes will be reverted)
    auto => {
        '!' => true,
        created_at => 'datetime!',
        changed_at => 'datetime!',  # Only tracks manual changes (nothing in auto)
        tagged => ['name', '!'],
    },
};

sub blank_item {
    my ($name) = @_;
    @_ == 1 or croak arity_miss 0+@_, 1;
    defined $name or croak "Name given to blank_item is undefined.";
    return {
        name => $name,
        tags => [],
        contents => [],
    };
};

sub deep_equals {
    my ($a, $b) = @_;
    ref $a ne ref $b and return 0;
    if (ref $a eq 'HASH') {
        for (keys %$a) {
            return 0 unless exists $b->{$_};
        }
        for (keys %$b) {
            return 0 unless exists $a->{$_};
            return 0 unless exists $a->{$_} and deep_equals($a->{$_}, $b->{$_});
        }
        return 1;
    }
    elsif (ref $a eq 'ARRAY') {
        return 0 unless @$a == @$b;
        for (0..$#$a) {
            return 0 unless deep_equals($a->[$_], $b->[$_]);
        }
        return 1;
    }
    elsif (defined $a) {
        return defined $b && $a eq $b;
    }
    else {
        return !defined $b;
    }
}

sub process_changes {
    my ($new, $old) = @_;
    if (!defined $new) {
        if (!defined $old) {
            return 0;
        }
         # TODO: delete
    }
    criticize_die $new, $item_schema, "Changed item $new->{name} did not pass validation.";
    if (defined $old) {
        $new->{auto} = $old->{auto};
        return 0 if deep_equals($new, $old);
    }
     # TODO: link tags
    $new->{auto}{changed_at} = iso_datetime(gmtime(now));
    if (!defined $old) {
        $new->{auto}{created_at} = $new->{auto}{changed_at};
    }
    return 1;
}


##### Backend

our $event_schema = {
    id => 'string!',
    started_at => 'datetime!',
    finished_at => 'datetime!',
    source => {
        '!' => true,
        interface => 'string!',
        '*' => '*?',
    },
    request => '*?!',
    response => '*?!',
    changes => { '*' => {
        previous => 'string?!',
        item => $item_schema,
    }},
};
 # Ad-hoc, doesn't conform to any standardized schema schema.
our $index_schema = {
    changed_at => 'datetime!',  # When this file specifically was last changed
    items => { '!' => true, '*' => 'string' },
};

 # Current state, loaded if necessary
my $index;
my %events;
 # Editable.  Changes will be validated then committed in one event.
my %write_items;
my $event;

 # Must wrap this around any access.
my $transaction;
sub READ () { return 1; }  # LOCK_SH
sub WRITE () { return 2; }  # LOCK_EX
sub transaction {
    my ($mode, $proc) = @_;
    @_ == 2 or arity_miss 0+@_, 1;
    $mode eq READ or $mode eq WRITE or die "Invalid transaction mode '$mode' (must be READ or WRITE)";
    if (defined $transaction) {
        $transaction == $mode or die "Cannot nest transactions of different types.";
        return $proc->();
    }
    $transaction = $mode;
     # Start
    mkdirs $datadir;
    open my $LOCK, '>', "$datadir/lock" or croak "Failed to acquire $datadir/lock: $!\n";
    flock $LOCK, $mode or croak "Failed to acquire $datadir/lock: $!\n";
    my @r;
    eval {
         # Generate necessary structure
        mkdirs "$datadir/events";
        if (-e "$datadir/index.json") {
            $index = decode_json slurp "$datadir/index.json";
             # Is this check worth the performance hit?  Profiling needed.
            criticize_die $index, $index_schema, "Internal error: index.json is not valid!";
        }
        else {
            $index = {
                latest_event => null,
                items => {},
            };
        }
        my $event = {
            started_at => iso_datetime(gmtime(now)),
            source => { interface => 'unknown' },  # TODO: read %ENV
            request => null,
            response => null,
        };
         # Do the actual code
        @r = $proc->();
         # Finish event structure
        $event->{id} = new_event_id() until !-e "$datadir/events/$event->{id}.json";
        my $event_filename = "$datadir/events/$event->{id}.json";
        open my $EVENT, '>', $event_filename or die "Could not open $event_filename for writing: $!";
        chmod 0440, $EVENT or die "Could not chmod event: $!\n";
        if ($transaction == WRITE) {
            my $changed = 0;
            $event->{changes} = {};  # Mark that it was a write transaction even if there are no changes
            for (keys %write_items) {
                if ($write_items{$_}{name} ne $_) {
                    die "Changing name of item is NYI, sorry.\n";
                }
                my $old_event = $events{$index->{items}{$_}};
                if (process_changes($write_items{$_}, $old_event->{changes}{$_}{item})) {
                    $changed = 1;
                    $event->{changes}{$_} = {
                        previous => defined $old_event ? $old_event->{id} : null,
                        item => $write_items{$_},
                    };
                    $index->{items}{$_} = $event->{id};
                }
            }
            $event->{finished_at} = iso_datetime(gmtime(now));
            criticize_die $event, $event_schema, "Internal error: tried to write invalid event!";
            if ($changed) {
                $index->{updated_at} = $event->{finished_at};
                criticize_die $index, $index_schema, "Internal error: tried to write invalid index!";
            }
             # Commit event
            print $EVENT encode_json $event;
            close $EVENT or die "Could not close $event_filename: $!\n";
             # Commit index
            if ($changed) {
                splat "$datadir/index.json", encode_json($index);
            }
        }
    };
    my $mess = $@;
     # Clear data
    $index = undef;
    %events = ();
    %write_items = ();
    $transaction = undef;
     # Release lock and finish dying if necessary
    flock $LOCK, 8 or $mess .= "Failed to release $datadir/lock: $!\n";
    close $LOCK or $mess .= "Failed to close $datadir/lock: $!\n";
    $mess and die $mess;
     # Annoying contextual return hack
    if (@r == 1) {
        return $r[0];
    }
    else {
        return @r;
    }
};

 # Returns a read-only event with the given id.  Dies if non-existent
sub event {
    my ($id) = @_;
    @_ == 1 or croak arity_miss 0+@_, 1;
    defined $id or croak "ID passed to event() as parameter is undefined.";
    defined $transaction or croak "event() called outside of a transaction.";
    if (!exists $events{$events{$id}}) {
        my $event = decode_json slurp "$datadir/events/$id";
        criticize_die $event, $event_schema, "Internal error: event $id had invalid data.";
        $events{$id} = $event;
    }
    return $events{$id};
}
 # Returns the event for this item, or undef if not registered
sub item_event {
    my ($name) = @_;
    @_ == 1 or croak arity_miss 0+@_, 1;
    defined $name or croak "Name passed to item_event() as parameter is undefined.";
    defined $transaction or croak "item_event() called outside of a transaction.";
    exists $index->{items}{$name} or return undef;
    my $event = event($index->{items}{$name});
    exists $event->{changes}{$name} or die "Internal error: Event $event->{id} was referred to for item $name, but it didn't contain it.\n";
    return $event;
}
 # Returns previous event *ID* for this event and item
sub item_event_previous {
    my ($name, $id) = @_;
    @_ == 2 or croak arity_miss 0+@_, 1;
    defined $name or croak "Name passed to item_event_previous() as first parameter is undefined.";
    defined $id or croak "ID passed to item_event_previous() as second parameter is undefined.";
    my $event = event($id);
    exists $event->{changes}{$name} or croak "Event and item passed to item_event_previous() don't match.";
    return $event->{changes}{$name}{previous};
}

 # Inside a transaction, returns a readable or read-writable item.
 # Returns undef if the item doesn't exist or is deleted.
sub item {
    my ($name) = @_;
    @_ == 1 or croak arity_miss 0+@_, 1;
    defined $name or croak "Name passed to item() as parameter is undefined.";
    if ($transaction == READ) {
        my $event = item_event($name) // return undef;
        return $event->{changes}{$name}{item};
    }
    elsif ($transaction == WRITE) {
        if (!exists $write_items{$name}) {
            my $event = item_event($name);
            if (defined $event) {
                $write_items{$name} = clone($event->{changes}{$name}{item});
            }
            else {
                $write_items{$name} = undef;
            }
        }
        return $write_items{$name};
    }
    else {
        croak "item('$name') called outside of a transaction.";
    }
}

 # Returns a list of all item names
sub all_names {
    @_ == 0 or croak arity_miss 0+@_, 0;
    defined $transaction or croak "all_names() called outside of a transaction.";
    return keys %{$index->{items}};
}
 # Returns a list of pairs of item names with items (aka a raw hash, not a reference)
sub all_items {
    @_ == 0 or croak arity_miss 0+@_, 0;
    defined $transaction or croak "all_items() called outside of a transaction.";
    return map ($_, item($_)), all_names;
}

sub validate_everything {
    @_ == 0 or croak arity_miss 0+@_, 0;
    croak "validate_everything is not yet implemented.";
}

1;
